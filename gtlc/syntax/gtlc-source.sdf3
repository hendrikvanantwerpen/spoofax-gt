module gtlc-source

imports Common types

sorts Expr context-free syntax
  
  Expr = <(<Expr>)> { bracket }
  
  Expr.Ascr = <<Expr> : <Type>>

  Expr.Unit  = <unit>

  Expr.True  = <true>
  
  Expr.False = <false>

  Expr.Pair = <(<Expr>, <Expr>)>
  
  Expr.Fst    = <fst <Expr>>
  Expr.Snd    = <snd <Expr>>
  Expr.IsUnit = <isunit <Expr>>
  Expr.IsBool = <isbool <Expr>>
  Expr.IsPair = <ispair <Expr>>
  Expr.IsRef  = <isref <Expr>>
  Expr.IsFun  = <isfun <Expr>>
  
  Expr.If = <
    if <Expr> then 
      <Expr> 
    else 
      <Expr>
  >
 
  Expr.App  = <<Expr> <Expr>> { left }
    
  Expr.Lam  = <\\<ID> : <Type>. <Expr>>
  
  Expr.LamD = <\\<ID>. <Expr>>
  
  Expr.Var  = <<ID>>
  
  Expr.LetD  = <
    let <ID> = <Expr> 
     in <Expr>
  >
  
  Expr.Let  = <
    let <ID> : <Type> = <Expr> 
     in <Expr>
  >
  
  Expr.Ref  = <ref <Expr>>
  
  Expr.Deref = <!<Expr>>
  
  Expr.Set  = <<Expr> := <Expr>>
  
  Expr.Seq  = <
    <Expr>; 
    <Expr>
  > {right}
  
context-free priorities

    Expr.App
  > {Expr.Fst Expr.Snd Expr.IsUnit Expr.IsBool Expr.IsPair Expr.IsRef Expr.IsFun}
  > {Expr.LamD Expr.Lam}
  > {Expr.Ref Expr.Deref}
  > Expr.Set
  > Expr.If
  > Expr.Seq
  > {Expr.Let Expr.LetD}
  > Expr.Ascr
  
template options

  ID = keyword { reject }
  keyword -/- [a-zA-Z0-9]
  
  
// context-free syntax // not in the source language
  //  Expr.Cast = <<Expr> : <Type> =\> <Type>>